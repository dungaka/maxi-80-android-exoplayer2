/*
 * Copyright 2018 Sascha Peilicke
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.github.dcendents.android-maven' version '2.0'
    id 'com.jfrog.bintray' version '1.8.0'
}

def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.file) {
    Properties localProperties = new Properties()
    localPropertiesFile.withInputStream { localProperties.load(it) }
    localProperties.each { key, value ->
        properties.ext.set(key, value)
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionName '1.0.1'
    }

    buildTypes {
        debug {
            consumerProguardFiles 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
}

dependencies {
    // Runtime dependencies
    implementation 'com.google.android.exoplayer:exoplayer-core:2.8.4'

    // Test dependencies
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation 'org.mockito:mockito-core:2.19.0'
}

group = 'saschpe.android'
version = android.defaultConfig.versionName

ext {
    siteUrl = 'https://github.com/saschpe/android-exoplayer2-ext-icy'
    gitUrl = 'https://github.com/saschpe/android-exoplayer2-ext-icy.git'
    descr = 'ExoPlayer2 Shoutcast Metadata Protocol (ICY) extension.'
}

bintray {
    user = project.properties["bintray.user"]
    key = project.properties["bintray.apikey"]

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'maven'
        name = 'android-exoplayer2-ext-icy'
        userOrg = 'saschpe'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/saschpe/android-exoplayer2-ext-icy/issues'
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        desc = descr
        labels = ['aar', 'android']
        publish = true
        publicDownloadNumbers = true

        githubRepo = 'saschpe/android-exoplayer2-ext-icy'
        githubReleaseNotesFile = 'README.md'

        version {
            name = android.defaultConfig.versionName
            desc = descr
            gpg {
                sign = true
            }
        }
    }
}

task createPom {
    pom {
        project {
            packaging 'aar'

            name project.name
            description descr
            url siteUrl
            inceptionYear '2018'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
            developers {
                developer {
                    id 'saschpe'
                    name 'Sascha Peilicke'
                    email 'sascha@peilicke.de'
                }
            }
        }
    }.writeTo("$buildDir/poms/pom-default.xml").writeTo("pom.xml")
}
build.dependsOn createPom

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
